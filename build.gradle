// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext.versions = [
            kotlin    : '1.3.50',
            buildTools: '29.0.2',
            minSdk    : 21,
            compileSdk: 29,
            targetSdk: 29
    ]

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6"
        classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.15.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.22.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.1"
}

allprojects {
    repositories {
        maven {  url 'https://dl.bintray.com/netguru/maven/' }
        jcenter()
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
        maven { url "https://clojars.org/repo/" }
        maven { url "https://maven.fabric.io/public" }
    }
}

tasks.register('detektAll', io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs detekt analysis over whole code base without the starting overhead for each module."
    parallel = true
    config = files("$rootDir/default-detekt-config.yml")
    setSource(files(projectDir))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
    reports {
        xml.enabled = true
        html.enabled = false
        txt.enabled = false
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

allprojects {
    tasks.withType(Javadoc) {
        excludes = ['**/*.kt'] // < ---- Exclude all kotlin files from javadoc file.
    }
}

apply from: 'buildsystem/dependencies.gradle'
apply from: 'buildsystem/bitrise.gradle'
apply from: 'buildsystem/secrets.gradle'
